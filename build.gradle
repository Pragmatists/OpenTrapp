buildscript {
    ext {
        springBootVersion = '2.0.4.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'com.pragmatists'
version = '1.0.0-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}
sourceSets {

    apiTest {

            compileClasspath += main.output
            runtimeClasspath += main .output
    }
}

configurations {
    apiTestCompile.extendsFrom testCompile
    apiTestRuntime.extendsFrom testRuntime
}

dependencies {
    testCompile sourceSets.main.output
    apiTestCompile sourceSets.main.output

    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'
    compile('org.springframework.boot:spring-boot-starter-data-mongodb')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-jetty')
    compile 'com.google.guava:guava:26.0-jre'
    testCompile('junit:junit:4.12')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    compile('de.flapdoodle.embed:de.flapdoodle.embed.mongo')
    testCompile('org.springframework.security:spring-security-test')
    testCompile('org.concordion:concordion:2.2.0')
    testCompile group: 'io.rest-assured', name: 'rest-assured', version: '3.1.0'

}

test {
    scanForTestClasses = true
    exclude '**/*Setup'
}


task apiTest(type: Test) {

    group 'API End-to-End tests'
    description = "Run End-to-End against REST API"

    systemProperty 'concordion.output.dir', "$buildDir/reports/api"
    testClassesDir = sourceSets.apiTest.output.classesDir
    classpath = sourceSets.apiTest.runtimeClasspath
    reports.html.destination = file("$reports.html.destination/api")
    reports.junitXml.destination = file("$reports.junitXml.destination/api")

    ignoreFailures = false
    exclude '**/*Fixture.class'
}

check.dependsOn apiTest
