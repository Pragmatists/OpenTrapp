buildscript{
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.ajoberstar:gradle-git:1.1.0'
    }
}

apply plugin: 'war'
apply plugin: 'jetty'
apply plugin: 'org.ajoberstar.github-pages'
apply plugin: 'java'

allprojects {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}

repositories { 

    mavenCentral()
}

ext {
    springVersion = '4.0.9.RELEASE'
    springSecurityVersion = '3.2.6.RELEASE'
    springDataVersion = '1.7.2.RELEASE'
    jacksonVersion = '1.9.12'
    jettyVersion = '7.6.0.v20120127'
    mongoDriver = '3.0.3'
}

sourceSets {

    api {
        java {
            srcDir file('src/test/api')
            compileClasspath += main.output
            runtimeClasspath += main.output
        }

        test.resources.srcDirs 'src/test/api'
        
        compileClasspath = compileClasspath + files(test.output.classesDir)
        runtimeClasspath = output + compileClasspath
    }
}

configurations {
    runtimeOnly
    apiCompile.extendsFrom testCompile
    apiRuntime.extendsFrom testRuntime
}

dependencies {
    
    compile 'javax.servlet:servlet-api:2.5'
    
    ['context', 'webmvc', 'orm', 'test'].each { module ->
        compile "org.springframework:spring-$module:$springVersion"
    }
    ['core', 'web', 'config', 'openid'].each { module ->
        compile ("org.springframework.security:spring-security-$module:$springSecurityVersion")
    }

    compile ("org.springframework.security.oauth:spring-security-oauth2:2.0.7.RELEASE")

    compile ("org.springframework:spring-context:$springVersion"){
        exclude group: 'commons-logging', module: 'commons-logging'
    }

    compile "org.codehaus.jackson:jackson-mapper-asl:$jacksonVersion"
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.0.5'
    compile 'com.google.guava:guava:r09'
    compile 'org.apache.commons:commons-lang3:3.0'
    compile 'com.googlecode.lambdaj:lambdaj:2.3.3'

    compile "org.mongodb:mongo-java-driver:$mongoDriver"
    compile("org.springframework.data:spring-data-mongodb:$springDataVersion") {
        exclude group: "org.mongodb", module: "mongo-java-driver"
    }
    compile 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:1.50.0'
    compile 'com.google.api-client:google-api-client:1.23.0'

    testCompile 'junit:junit:4.11'
    testCompile 'org.assertj:assertj-core:1.5.0'
    testCompile 'org.mockito:mockito-all:1.9.5'
    testCompile 'com.googlecode.catch-exception:catch-exception:1.2.0'
    testCompile 'org.concordion:concordion:1.4.4'
    testCompile 'com.jayway.restassured:rest-assured:2.0.1'
    
    testCompile "org.eclipse.jetty:jetty-servlet:$jettyVersion"
    testCompile "org.eclipse.jetty:jetty-webapp:$jettyVersion"
    runtimeOnly "org.mortbay.jetty:jetty-runner:$jettyVersion"
    
    compile (
        [group: 'org.slf4j', name: 'jcl-over-slf4j', version: '1.6.2'],
        [group: 'ch.qos.logback', name: 'logback-core', version: '1.0.0'],
        [group: 'ch.qos.logback', name: 'logback-classic', version: '1.0.0']
        )
}

jettyRun.contextPath = ''


task apiTest(type:Test) {
    
    ignoreFailures = true
    systemProperty 'concordion.output.dir', "$buildDir/reports/api-specification"
    description = "Run acceptance tests against REST API"
    testClassesDir = project.sourceSets.api.output.classesDir
    classpath = project.sourceSets.api.runtimeClasspath + files("$rootDir/src/test/api")
    exclude '**/*Fixture.class'
}

task stage(dependsOn: ['clean', 'war', 'copyJettyRunner'])

task copyJettyRunner << {
    copy {
      from configurations.runtimeOnly.copy().setTransitive(false)
      into "$buildDir/libs"
      rename { name ->
        def artifacts = configurations.runtimeOnly.resolvedConfiguration.resolvedArtifacts
        def artifact = artifacts.find { it.file.name == name }
        "${artifact.name}.${artifact.extension}"
      }
    }
  }

test.dependsOn apiTest
test {
    scanForTestClasses = true
    exclude '**/*Setup'
}

githubPages {
    pages {
        from 'build/reports/api-specification'
    }
}

task useMpi << {
    githubPages.repoUri = 'git@github.com:mpi/OpenTrApp.git'
}

task useProd << {
    githubPages.repoUri = 'git@github.com:Pragmatists/OpenTrapp.git'
}

task publishConcordion(dependsOn: [apiTest, publishGhPages]) {}
